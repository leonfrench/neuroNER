PACKAGE neuroner;

//UIMAFIT 

// NEURON
DECLARE Neuron, NeuronProperty;
"\\b((inter)?neurone?s?|cells?)\\b" -> Neuron;
//"\\b((inter)?neurons?)\\b" -> Neuron;

// ORIENTATION
DECLARE NeuronProperty Orientation;
"inverted|horizontal|descending" -> Orientation;

// MORPHOLOGY
DECLARE NeuronProperty Morphology;
WORDLIST morphology = 'morphology_manual.txt';
Document{-> MARKFAST(Morphology, morphology)};

// SIZE
DECLARE NeuronProperty Size;
"large|medium|small|narrow|giant" -> Size;

// PROTEIN
DECLARE NeuronProperty ProteinProp;
DECLARE ProteinName, ProteinTrigger;
WORDLIST proteinNames = 'proteins_list.twl';
WORDLIST proteinNames_manual = 'proteins_ns_manual.txt';
Document{-> MARKFAST(ProteinName, proteinNames)};
Document{-> MARKFAST(ProteinName, proteinNames_manual)};
"expressing|principal|containing|immuno(nega|posi|reac)tive|positive" -> ProteinTrigger;
(ProteinName){-> MARK(ProteinProp, 1, 2)} ("-"? ProteinTrigger);

// BRAINREGION
DECLARE NeuronProperty BrainRegionProp; // so as not to be confused w/ other BrainRegion
WORDLIST brainRegions = 'regions_list.twl';
WORDLIST brainRegions_manual = 'regions_manual.txt';
Document{-> MARKFAST(BrainRegionProp, brainRegions)};
Document{-> MARKFAST(BrainRegionProp, brainRegions_manual)};

// NEUROTRANSMITTER 
DECLARE NeuronProperty NeurotransmitterProp;
"(gaba|GABA|glutamat|dopamin)ergic" -> NeurotransmitterProp;

// FUNCTION 
DECLARE NeuronProperty Function;
WORDLIST functions = 'functions_manual.txt';
Document{-> MARKFAST(Function, functions)};

// LAYER
DECLARE NeuronProperty Layer;
"[Ll]ayer ([1-6]([/-][1-6])?|[iIvV]+([/-][iIvV]+)?)" -> Layer;
    
// ELECTROPYHSIOLOGY
DECLARE NeuronProperty Electrophysiology;
"((continu(ally|ous)?|delayed) )?((regular|fast|late|burst(ing)?|classical|delayed)[ -])?((non[- ]+)?accomm?odating|spiking|firing|irregular|stuttering)" -> Electrophysiology;

// gather NeuronProperty occuring before Neuron
DECLARE NeuronWithProperties;
NeuronProperty+{-> MARK(NeuronWithProperties, 1, 2)} Neuron;
(NeuronProperty+ (COMMA | "and" | (COMMA "and")))* {-> MARK(NeuronWithProperties, 1, 3)} NeuronProperty+ Neuron;
// keep only longest NeuronWithProperties
NeuronWithProperties{PARTOFNEQ(NeuronWithProperties) -> DEL};
// TODO remove cells without NeuronProperty

// Find up to 3 unmatched words preceding a NeuronWithProperties to see what is still missing
// DECLARE Sentence; PERIOD #{-> MARK(Sentence)} PERIOD;
DECLARE Missing, InBetweenMissing;
(W{-PARTOF(NeuronProperty)}){-> MARK(Missing, 1, 1)} NeuronWithProperties;
W{-PARTOF(NeuronProperty)} Missing{-> SHIFT(Missing, 1, 2)};
W{-PARTOF(NeuronProperty)} Missing{-> SHIFT(Missing, 1, 2)};
W{-PARTOF(NeuronProperty)} Missing{-> SHIFT(Missing, 1, 2)};

